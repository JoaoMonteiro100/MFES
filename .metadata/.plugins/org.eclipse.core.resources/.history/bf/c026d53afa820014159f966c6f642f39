class ExchangeSystem
/*
	Defines the state variables and operations available to the users (both sellers and buyers).
	Class 5, T3, FEUP, MFES, 2014/2015
*/
	types
		public String = seq of char;
		
	instance variables
		public sellOrders: seq of SellOrder := [];
		public buyOrders: seq of BuyOrder := [];
		public so: SellOrder;
		public bo: BuyOrder;	

	operations
		public ExchangeSystem() res: ExchangeSystem ==
		(
			return self;
		);
		
		--------------------- SELLER OPERATIONS ---------------------
		
		/*
		* Enables sellers to post their cars to the exchange system
		*/
		public addSellOrder(m:Car`CarModel, c:Car`CarColor, y:nat1, mi:nat, p:real, mbs:nat1, os:nat1) res: SellOrder ==
    (
     	so := new SellOrder(m,c,y,mi,p,mbs,os);
      placeSellOrder(so);
      return so;
    )
    ext wr so;
    
    /*
		* Takes a sell order and puts it in the vector with all the sell orders
		*/
    public placeSellOrder(so:SellOrder) ==
    (
   		so.setID(len sellOrders + 1);
   		sellOrders := sellOrders ^ [so];
    )
    ext wr sellOrders;
   -- post sellOrders = sellOrders~ ^ [so];
    
    /*
		* Cancels a sell order (NOTE: it doesn't delete it, just changes its status)
		*/   
    public cancelSale(id:nat1) res: bool ==
		(
			if sellOrders(id).status <> <Canceled> then sellOrders(id).status := <Canceled>;
			return true;
		)
		ext wr sellOrders
		pre len sellOrders > 0;
	
		/*
		* Lists all the sell orders
		*/
		public listSales() ==
		(
			for i = 1 to len sellOrders do
				IO`println(sellOrders(i));
		)
		ext rd sellOrders
		pre len sellOrders > 0;
		
		/*
		* Edit of an existing sale order (its ID is mandatory)
		*/
		public updateSale(i:nat1, m:Car`CarModel, c:Car`CarColor, y:nat, mi:nat, p:real, mbs:nat, os:nat) res:bool ==
		(
			if m <> "" 
				then sellOrders(i).car.model := m;
			if c <> <Null>	
				then sellOrders(i).car.color := c;
			if y <> 0
				then sellOrders(i).car.year := y;
			if mi <> 0	
				then sellOrders(i).car.mileage := mi;
			if p <> 0.0
				then sellOrders(i).car.price := p;
			if mbs <> 0
				then sellOrders(i).minBuySize := mbs;
			if os <> 0
				then sellOrders(i).maxBuySize := os;
			return true;
		)
		ext wr sellOrders
		pre len sellOrders > 0;
		
		

    --------------------- BUYER OPERATIONS ---------------------
    
    /*
		* Enables buyers to post their orders to the exchange system
		*/
    public addBuyOrder(m:Car`CarModel, c:Car`CarColor, y:nat, mi:nat, mp:real, q:nat) res: BuyOrder ==
    (
	      bo := new BuyOrder(m,c,y,mi,mp,q);
	      placeBuyOrder(bo);
	      checkSellOrders(bo);
	      return bo;
     )
     ext wr bo;
    
    /*
		* Takes a buy order and puts it in the vector with all the buy orders
		*/
    public placeBuyOrder(bo:BuyOrder) ==
    (
    	bo.setID(len buyOrders + 1);
    	buyOrders := buyOrders ^ [bo];
    )
    ext wr buyOrders;
    --post buyOrders = buyOrders ^ [bo];
    
    /*
		* Cancels a buy order (NOTE: it doesn't delete it, just changes its status)
		*/   
    public cancelPurchase(id:nat1) res: bool ==
    (
    	if buyOrders(id).status <> <Canceled> then buyOrders(id).status := <Canceled>;
			return true;
		)
		ext wr buyOrders
		pre len buyOrders > 0;
    
    /*
		* Lists all the buy orders
		*/
    public listPurchases() ==
		(
			for i = 1 to len buyOrders do
				IO`println(buyOrders(i));
		)
		ext rd buyOrders
		pre len buyOrders > 0;
		
		/*
		* Edit of an existing buy order (its ID is mandatory)
		*/
		public updatePurchase(i:nat1, m:String, c:Car`CarColor, y:nat, mi:nat, mp:real, q:nat) res:bool ==
		(
			if m <> "" 
				then buyOrders(i).model := m;
			if c <> <Null>	
				then buyOrders(i).color := c;
			if y <> 0
				then buyOrders(i).minYear := y;
			if mi <> 0	
				then buyOrders(i).maxMileage := mi;
			if mp <> 0.0
				then buyOrders(i).maxPrice := mp;
			if q <> 0
				then buyOrders(i).quantity := q;
			return true;
		)
		ext wr buyOrders
		pre len buyOrders > 0;
		
    
    --------------------- MATCHING OPERATIONS ---------------------
    
    /*
		* Checks if there are any sell order that match the buy order given as an argument
		*/
    public checkSellOrders(bo:BuyOrder) ==
    	-- go through the sell orders vector
    	for i = 1 to len sellOrders do
    	(
    		-- if the quantity ordered is at least as big as the minimum buy size...
    		if sellOrders(i).minBuySize <= bo.quantity then
	    	(
	    		-- ...and the model of both orders match (or the buy order doesn't specify the model)...
	    		if sellOrders(i).car.model = bo.model or bo.model = "" then
	    		(
	    			-- ...and the color of both orders match (or the buy order doesn't specify the color)...
	    			if sellOrders(i).car.color = bo.color or bo.color = <Null> then
	    			(
	    				-- ...and the year of the sell order is more recent than (or equal to) what the buy order asks for...
	    				if sellOrders(i).car.year >= bo.minYear then
	    				(
	    					-- ...and the mileage of the sell order is less than (or equal to) what the buy order asks for...
	    					if sellOrders(i).car.mileage <= bo.maxMileage then
	    					(
	    						-- ...and the price asked for the car is less than what the buyer is willing to pay...
	    						if sellOrders(i).car.price <= bo.maxPrice then
	    						(
	    							-- ...then there is a transaction.
	    							sellOrders(i).status := <Sold>;
	    							bo.status := <Purchased>;
	    							
	    							IO`print("Purchase proposal number ");
	    							IO`print(bo.id);
	    							IO`print("assigned to sales number ");
	    							IO`print(sellOrders(i).id);
										IO`print("\n\n");
	    						)
	    					)
	    				)
	    			)
    			)
    		)
    	)
    	ext wr sellOrders, buyOrders, bo, so
    	pre len sellOrders > 0;
    
end ExchangeSystem